// Used for building a runnable jar
plugins {
    id 'java'
    id 'base'
}

// Where the code is
sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
}

// Where to find patched_client.jar
repositories {
    flatDir {
        dirs './'
    }
}

// The patched_client dependency
dependencies {
    implementation name: 'patched_client'
}

jar {
    // Starting class
    manifest {
        attributes 'Main-Class': 'bot.Main'
    }

    // Include all the source files
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

// Moves the compiled jar to the root folder
base {
    archivesName = "IdleRSC"
    libsDirectory = getProjectDir()
}

// Needed as Main uses the class files within scripting to populate the
// script table
tasks.register("copyScriptClasses", Copy) {
    from "./build/classes/java/main/scripting"
    into "./bin/scripting"
    dependsOn jar
}

// Deletes the build directory
tasks.register("cleanBuildDir", Delete) {
    delete 'build'
    dependsOn copyScriptClasses
}

// Ensure all tasks are completed when running build by making build require
// the last task in a dependency chain
build.dependsOn(cleanBuildDir)
